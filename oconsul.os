#Использовать json

Перем ПортConsul;
Перем ТокенACL;

// Программный интерфейс

Функция ЗначениеПоКлючу(Ключ, ДесериализоватьИзJSON = Ложь) Экспорт
	Ответ = ОтветConsul("kv/" + Ключ);
	Если Ответ.КодСостояния = 200 Тогда
		СтрокаОтвета = Ответ.ПолучитьТелоКакСтроку(КодировкаТекста.UTF8);
		СтруктураОтвета = ДесериализоватьJSON(СтрокаОтвета);
		Результат = ДекодироватьBase64(СтруктураОтвета[0].Получить("Value"));
		Если ДесериализоватьИзJSON Тогда
			Результат = ДесериализоватьJSON(Результат);
		КонецЕсли;
	Иначе
		Результат = Неопределено;
	КонецЕсли;
	Возврат Результат;
КонецФункции
	
Функция УстановитьЗначениеКлюча(Ключ, Значение) Экспорт
	Ответ = ОтветConsul("kv/" + Ключ, Значение, "PUT");
	Возврат Ответ.КодСостояния = 200 И Ответ.ПолучитьТелоКакСтроку() = "true";
КонецФункции

// Служебные методы

Функция ОтветConsul(Ресурс, ТелоЗапроса = "", Метод = "GET")
	Соединение = Новый HTTPСоединение("127.0.0.1", ПортConsul);
	Запрос = Новый HTTPЗапрос("/v1/" + Ресурс);
	Если ЗначениеЗаполнено(ТелоЗапроса) Тогда
		Запрос.УстановитьТелоИзСтроки(ТелоЗапроса, КодировкаТекста.UTF8);
	КонецЕсли;
	Ответ = Соединение.ВызватьHTTPМетод(Метод, Запрос);
	Возврат Ответ;
КонецФункции

Функция ДекодироватьBase64(СтрокаBase64)
	ДвоичныеДанные = Base64Значение(СтрокаBase64);
	Чтение = Новый ЧтениеДанных(ДвоичныеДанные);
	Результат = Чтение.ПрочитатьСимволы(ДвоичныеДанные.Размер());
	Чтение.Закрыть();
	Возврат Результат;
КонецФункции

Функция ДесериализоватьJSON(СтрокаJSON)
	Парсер = Новый ПарсерJSON;
	Результат = Парсер.ПрочитатьJSON(СтрокаJSON);
	Возврат Результат;
КонецФункции

ПортConsul = 8500;
ТокенACL = "";
