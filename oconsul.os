#Использовать json

// Программный интерфейс

Функция ПолучитьЗначениеПоКлючу(Ключ, ДесериализоватьИзJSON = Ложь) Экспорт
	Соединение = Новый HTTPСоединение("127.0.0.1", 8500);
	Запрос = Новый HTTPЗапрос("/v1/kv/" + Ключ);
	Ответ = Соединение.ВызватьHTTPМетод("GET", Запрос);
	Если Ответ.КодСостояния = 200 Тогда
		СтрокаОтвета = Ответ.ПолучитьТелоКакСтроку(КодировкаТекста.UTF8);
		СтруктураОтвета = ДесериализоватьJSON(СтрокаОтвета);
		Результат = ДекодироватьBase64(СтруктураОтвета[0].Получить("Value"));
		Если ДесериализоватьИзJSON Тогда
			Результат = ДесериализоватьJSON(Результат);
		КонецЕсли;
	Иначе
		Результат = Неопределено;
	КонецЕсли;
	Возврат Результат;
КонецФункции
	
Функция УстановитьЗначениеКлюча(Ключ, Значение) Экспорт
	Соединение = Новый HTTPСоединение("127.0.0.1", 8500);
	Запрос = Новый HTTPЗапрос("/v1/kv/" + Ключ);
	Запрос.УстановитьТелоИзСтроки(Значение, КодировкаТекста.UTF8);
	Ответ = Соединение.ВызватьHTTPМетод("PUT", Запрос);
	Возврат Ответ.КодСостояния = 200 И Ответ.ПолучитьТелоКакСтроку() = "true";
КонецФункции

// Служебные методы

Функция ДекодироватьBase64(СтрокаBase64)
	ДвоичныеДанные = Base64Значение(СтрокаBase64);
	Чтение = Новый ЧтениеДанных(ДвоичныеДанные);
	Результат = Чтение.ПрочитатьСимволы(ДвоичныеДанные.Размер());
	Чтение.Закрыть();
	Возврат Результат;
КонецФункции

Функция ДесериализоватьJSON(СтрокаJSON)
	Парсер = Новый ПарсерJSON;
	Результат = Парсер.ПрочитатьJSON(СтрокаJSON);
	Возврат Результат;
КонецФункции
